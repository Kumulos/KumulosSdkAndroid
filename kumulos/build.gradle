apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def releaseVersion = System.getenv("TRAVIS_TAG") as String ?: "0.0.0"

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode System.getenv("TRAVIS_BUILD_NUMBER") as Integer ?: 0
        versionName releaseVersion

        setProperty("archivesBaseName", "${archivesBaseName}-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation "ch.acra:acra-http:5.0.1"
    implementation 'org.codehaus.jackson:jackson-mapper-asl:1.8.5'
    implementation 'com.google.android.gms:play-services-gcm:15.0.0'
}

android.testVariants.all {
    it.mergedFlavor.manifestPlaceholders = [ kumulos_gcm_sender_id: ""]
}

publishing {
    publications {
        Debug(MavenPublication) {
            artifact "$buildDir/outputs/aar/kumulos-$releaseVersion-debug.aar"
            groupId 'com.kumulos.android'
            artifactId 'kumulos-android-debug'
            version releaseVersion

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }

        Release(MavenPublication) {
            artifact "$buildDir/outputs/aar/kumulos-$releaseVersion-release.aar"
            groupId 'com.kumulos.android'
            artifactId 'kumulos-android-release'
            version releaseVersion

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = [ 'Debug', 'Release' ]

    publish = true

    pkg {
        repo = 'maven'
        name = 'kumulos-android-sdk'
        userOrg = 'kumulos'
        licenses = ['MIT']

        version {
            name = releaseVersion
            vcsTag = releaseVersion
        }
    }

}
