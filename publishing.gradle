apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin('com.android.library')) {
        from android.sourceSets.main.java.srcDirs
//        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
//        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

ext['signing.keyId'] = ''
ext['signing.password'] = ''
ext['signing.secretKeyRingFile'] = ''
ext['sonatypeUsername'] = ''
ext['sonatypePassword'] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is -> p.load(is) }
    p.each { name, value -> ext[name] = value }
} else {
    ext['signing.keyId'] = System.getenv('SIGNING_KEY_ID')
    ext['signing.password'] = System.getenv('SIGNING_PASSWORD')
    ext['signing.secretKeyRingFile'] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext['sonatypeUsername'] = System.getenv('SONATYPE_USERNAME')
    ext['sonatypePassword'] = System.getenv('SONATYPE_PASSWORD')
}

publishing {
    publications {
        debug(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId "${PUBLISH_ARTIFACT_ID}-debug"
            version PUBLISH_VERSION

            artifact("$buildDir/outputs/aar/${project.getName()}-${version}-debug.aar")
            artifact androidSourcesJar

            pom {
                name = PUBLISH_ARTIFACT_ID
                description = 'Official Android SDK for integrating Kumulos services with your mobile apps'
                url = 'https://github.com/Kumulos/KumulosSdkAndroid'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/Kumulos/KumulosSdkAndroid/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'kumulos'
                        name = 'Kumulos Ltd'
                        email = 'support@kumulos.com'
                    }
                }

                scm {
                    connection = 'scm:git:github.com/Kumulos/KumulosSdkAndroid.git'
                    developerConnection = 'scm:git:ssh://github.com/Kumulos/KumulosSdkAndroid.git'
                    url = 'https://github.com/Kumulos/KumulosSdkAndroid/tree/master'
                }

                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }

        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId "${PUBLISH_ARTIFACT_ID}-release"
            version PUBLISH_VERSION

            artifact("$buildDir/outputs/aar/${project.getName()}-${version}-release.aar")
            artifact androidSourcesJar

            pom {
                name = PUBLISH_ARTIFACT_ID
                description = 'Official Android SDK for integrating Kumulos services with your mobile apps'
                url = 'https://github.com/Kumulos/KumulosSdkAndroid'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/Kumulos/KumulosSdkAndroid/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'kumulos'
                        name = 'Kumulos Ltd'
                        email = 'support@kumulos.com'
                    }
                }

                scm {
                    connection = 'scm:git:github.com/Kumulos/KumulosSdkAndroid.git'
                    developerConnection = 'scm:git:ssh://github.com/Kumulos/KumulosSdkAndroid.git'
                    url = 'https://github.com/Kumulos/KumulosSdkAndroid/tree/master'
                }

                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'mavenCentral'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications
}
